buildscript {
  repositories {
    add(new org.apache.ivy.plugins.resolver.URLResolver()) {
      name = "GitHub"
      addArtifactPattern "http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]"
    }
    maven {
      name "zt-public-snapshots"
      url "http://repos.zeroturnaround.com/nexus/content/groups/zt-public/"
    }
    mavenCentral()
  }

  dependencies {
    classpath "bmuschko:gradle-tomcat-plugin:0.8.2"
    classpath "org.zeroturnaround:gradle-jrebel-plugin:1.0.2-SNAPSHOT"
  }
}

apply plugin: "rebel"
apply plugin: "java"
apply plugin: "war"
apply plugin: "tomcat"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "eclipse"

repositories {
  add(new org.apache.ivy.plugins.resolver.URLResolver()) {
    name = "Edify S3"
    addArtifactPattern "https://s3.amazonaws.com/edify-soft/[organisation]/[module]/[module]-[revision].[ext]"
  }
  maven {
    url "http://repo.springsource.org/release"
  }
  maven {
    url "http://repo.springsource.org/milestone"
  }
  mavenCentral()
}

version = "1.0-SNAPSHOT"
group = "change.me"
projectName = "changeme"

targetCompatibility = "1.6"
sourceCompatibility = "1.6"

configurations {
  all*.exclude group: "commons-logging"
  all*.exclude module: "log4j"
  yui
}

war {
  baseName = projectName
  dependsOn += ["compressAll"]
}

tomcatRun {
  contextPath = "/"
  dependsOn += ["generateRebel"]
}

task compileLess << {
  //Compile all less files
  println "\u001b[0;32m===> Compiling Less files\u001b[m"
  workingDir = "src/main/webapp/WEB-INF/less"
  ["application.less"].each {
    filename = ((new File(it)).name =~ /^(.+)\.less/)[0][1]
    out = new File("src/main/webapp/css/${filename}.css")
    out.write("lessc $it".execute([], new File(workingDir)).getText())
  }
  //Compile bootstrap
  workingDir += "/bootstrap"
  out = new File("src/main/webapp/css/bootstrap.css")
  out.write("lessc bootstrap.less".execute([], new File(workingDir)).getText())
}

task compressCss(dependsOn: [":compileLess"]) << {
  println "\u001b[0;32m===> Compressing CSS\u001b[m"
  ant {
    taskdef(name: "yuicompressor",
            classname: "net.noha.tools.ant.yuicompressor.tasks.YuiCompressorTask",
            classpath: configurations.yui.asPath)
    yuicompressor(jsSuffix: "-min.js", cssSuffix: "-min.css", fromDir: "src/main/webapp/css", toDir: "src/main/webapp/css", warn: false) {
      include(name: "*.css")
      exclude(name: "*-min.css")
    }
  }
}

task compileCoffeScript << {
  println "\u001b[0;32m===> Compiling CoffeScript\u001b[m"
  workingDir = "src/main/webapp/WEB-INF/coffeescript"
  println "coffee --compile --output src/main/webapp/js $workingDir".execute().getText()
}

task compressJs(dependsOn: [":compileCoffeScript"]) << {
  println "\u001b[0;32m===> Compressing Javascript\u001b[m"
  ant {
    taskdef(name: "yuicompressor",
            classname: "net.noha.tools.ant.yuicompressor.tasks.YuiCompressorTask",
            classpath: configurations.yui.asPath)
    ["src/main/webapp/js"].each {
      yuicompressor(jsSuffix: "-min.js", cssSuffix: "-min.css", fromDir: it, toDir: it, warn: false) {
        include(name: "*.js")
        exclude(name: "*-min.js")
      }
    }
  }
}

task compressAll(dependsOn: [":compressCss", ":compressJs"])

processResources {
  filter(org.apache.tools.ant.filters.ReplaceTokens,
          tokens: [
                  LOGBACK_SYSLOG_HOST: LOGBACK_SYSLOG_HOST,
                  LOGBACK_SYSLOG_PORT: LOGBACK_SYSLOG_PORT,
                  LOGBACK_APPENDER: LOGBACK_APPENDER,
                  LOGBACK_LOG_FILE_LOCATION: LOGBACK_LOG_FILE_LOCATION,
                  HIBERNATE_DIALECT: HIBERNATE_DIALECT,
                  DATABASE_PASSWORD: DATABASE_PASSWORD,
                  DATABASE_URL: DATABASE_URL,
                  DATABASE_USERNAME: DATABASE_USERNAME,
                  DATABASE_DRIVER_CLASSNAME: DATABASE_DRIVER_CLASSNAME,
          ])
}

//*** Versions ***
SPRING_VERSION = "3.1.1.RELEASE"
SPRING_SECURITY_VERSION = "3.1.0.RELEASE"
TILES_VERSION = "2.2.2"
SLF4J_VERSION = "1.6.4"
HIBERNATE_VERSION = "4.1.0.Final"

hibernate = [
        "org.hibernate:hibernate-core:$HIBERNATE_VERSION",
        "org.hibernate:hibernate-validator:4.2.0.Final",
        "org.hibernate:hibernate-entitymanager:$HIBERNATE_VERSION",
        "org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final"]

dependencies {
  groovy("org.codehaus.groovy:groovy-all:1.8.5")
  providedCompile("javax.servlet:servlet-api:2.5", "javax.el:el-api:2.2", "javax.servlet.jsp:jsp-api:2.1")
  testCompile("junit:junit:4.10", "org.easymock:easymock:3.1")
  // ** Hibernate **
  hibernate.collect {
    compile(it) {
      exclude(group: "cglib", module: "cglib")
      exclude(group: "org.jboss.spec.javax.transaction", module: "jboss-transaction-api_1.1_spec")
    }
  }
  compile("org.springframework.security.oauth:spring-security-oauth:1.0.0.M5") {
    exclude(group: "org.springframework.security")
    exclude(group: "org.springframework")
  }
  // ** Spring **
  compile "org.springframework:spring-core:$SPRING_VERSION"
  compile "org.springframework:spring-context:$SPRING_VERSION"
  compile "org.springframework:spring-aop:$SPRING_VERSION"
  compile "org.springframework:spring-aspects:$SPRING_VERSION"
  compile "org.springframework:spring-tx:$SPRING_VERSION"
  compile "org.springframework:spring-jdbc:$SPRING_VERSION"
  compile "org.springframework:spring-orm:$SPRING_VERSION"
  compile "org.springframework:spring-web:$SPRING_VERSION"
  compile "org.springframework:spring-webmvc:$SPRING_VERSION"
  compile "org.springframework:spring-context-support:$SPRING_VERSION"
  compile "org.springframework:spring-test:$SPRING_VERSION"
  // ** AspectJ **
  compile "org.aspectj:aspectjrt:1.6.12"
  // ** Spring Security **
  compile "org.springframework.security:spring-security-core:$SPRING_SECURITY_VERSION"
  compile "org.springframework.security:spring-security-config:$SPRING_SECURITY_VERSION"
  compile "org.springframework.security:spring-security-web:$SPRING_SECURITY_VERSION"
  // ** Spring Data JPA **
  compile "org.springframework.data:spring-data-jpa:1.0.2.RELEASE"
  // ** Scribe OAuth **
  compile "org.scribe:scribe:1.3.0"
  // ** Apache Tiles **
  compile "org.apache.tiles:tiles-core:$TILES_VERSION"
  compile "org.apache.tiles:tiles-jsp:$TILES_VERSION"
  // ** Apache Commons **
  compile "commons-fileupload:commons-fileupload:1.2.2"
  compile "commons-digester:commons-digester:2.1"
  compile "commons-lang:commons-lang:2.6"
  compile "commons-pool:commons-pool:1.6"
  compile "commons-codec:commons-codec:1.6"
  // ** BoneCP **
  compile "com.jolbox:bonecp:0.7.1.RELEASE"
  // ** FlyWay **
  compile "com.googlecode.flyway:flyway-core:1.5"
  // ** SLF4J **
  compile "org.slf4j:slf4j-api:$SLF4J_VERSION"
  runtime "org.slf4j:jcl-over-slf4j:$SLF4J_VERSION"
  runtime "org.slf4j:slf4j-log4j12:$SLF4J_VERSION"
  // ** LogBack **
  compile "ch.qos.logback:logback-core:1.0.0"
  compile "ch.qos.logback:logback-classic:1.0.0"
  // ** Other **
  compile "joda-time:joda-time:2.0"
  compile "org.codehaus.jackson:jackson-mapper-asl:1.9.3"
  compile "cglib:cglib-nodep:2.2.2"
  // ** Database **
  compile "mysql:mysql-connector-java:5.1.18"
  // ** Javax **
  compile "javax.mail:mail:1.4.1"
  compile "javax.activation:activation:1.1.1"
  compile "javax.servlet:jstl:1.2"
  compile "javax.transaction:jta:1.1"
  compile "javax.validation:validation-api:1.0.0.GA"
  // ** Web Filters **
  compile "net.sf.ehcache:ehcache-web:2.0.4"
  compile "org.tuckey:urlrewritefilter:3.2.0"

  runtime "janino:janino:2.5.10"

  def tomcatVersion = "6.0.35"
  tomcat "org.apache.tomcat:catalina:${tomcatVersion}"
  tomcat "org.apache.tomcat:coyote:${tomcatVersion}"
  tomcat "org.apache.tomcat:jasper:${tomcatVersion}"
  yui "com.yahoo.platform.yui:yuicompressor:2.4.6"
  yui "noha:yui-compressor-ant-task:0.5.1"
}
